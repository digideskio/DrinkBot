buildscript {
  dependencies {
    classpath rootProject.ext.androidPlugin
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    classpath 'me.tatarka:gradle-retrolambda:3.1.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: rootProject.file('gradle/android-findbugs.gradle')
apply plugin: 'me.tatarka.retrolambda'

dependencies {
  // Android dependencies.
  compile rootProject.ext.supportV4
  compile rootProject.ext.supportAnnotations

  // Square dependencies.
  compile rootProject.ext.dagger
  apt rootProject.ext.daggerCompiler

  compile rootProject.ext.sqlBrite

  // Third-party dependencies.
  compile rootProject.ext.butterKnife
  compile rootProject.ext.timber
  compile rootProject.ext.rxAndroid
  compile rootProject.ext.autoParcel
  apt rootProject.ext.autoParcelProcessor

  provided rootProject.ext.findbugsJsr305
  provided rootProject.ext.findbugsAnnotations
}

def isCi() {
  return "true".equals(System.getenv('CI'))
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    textOutput 'stdout'
  }

  dexOptions {
    preDexLibraries = !isCi()
  }

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 22
    applicationId 'com.f2prateek.drinkbot'

    versionCode 100
    versionName '1.0.0'
  }

  signingConfigs {
    debug {
      storeFile file('debug.keystore')
      storePassword 'android'
      keyAlias 'android'
      keyPassword 'android'
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.development'
      signingConfig signingConfigs.debug
    }
  }
}
